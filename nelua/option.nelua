## local function make_option(T)
  ## static_assert(traits.is_type(T), "invalid type '%s'", T)
  local T: type = @#[T]#

  local OptionT: type <nickname(#[string.format("Option(%s)", T)]#)> = @record{
    some: boolean,
    value: T
  }

  ## OptionT.value.is_option = true
  ## OptionT.value.subtype = T

  function OptionT:set(v: T)
    self.some = true
    self.value = v
  end

  function OptionT:get(): T
    assert(self.some)
    return self.value
  end

  function OptionT:get_or(v: T): T
    return self.some and self:get() or v
  end

  function OptionT:is_some(): boolean
    return self.some
  end

  function OptionT:is_none(): boolean
    return not self.some
  end

  function OptionT:is_some_val(v: auto): boolean
    return self:is_some() and self:get() == v
  end

  local OptionT_convertible_concept = #[concept(function(v) 
    return v.type.is_option and v.type.subtype.is_niltype
  end)]#

  function OptionT.__convert(v: OptionT_convertible_concept): OptionT <inline>
    return {}
  end

  ## return OptionT
## end

local Option = #[generalize(make_option)]#

function Option.Some(v: auto): Option(#[v.type]#)
  local val: Option(#[v.type]#) 
  val:set(v)
  return val
end

function Option.None(): Option(niltype)
  return {}
end

return Option
